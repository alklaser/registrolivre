buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
        classpath("org.flywaydb:flyway-gradle-plugin:3.2.1")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.flywaydb:flyway-core:3.2.1")
    compile("org.hibernate:hibernate-core:4.3.10.Final")
    compile("org.projectlombok:lombok:1.16.4")
    compile("org.postgresql:postgresql:9.4-1201-jdbc41")
    compile "org.apache.commons:commons-dbcp2:2.1"

    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-all:1.9.5")
}

task npmInstall(type: Exec) {
    commandLine "npm", "install"
}

task deleteNodeFolder(type: Delete) {
    delete "node_modules"
}

task npmTest(type: Exec) {
    commandLine "npm", "test"
}

task initDatabase(type: Exec) {
    description = 'Configure database'
    commandLine 'scripts/./install_database.sh'
}

task jsTest(dependsOn: ['deleteNodeFolder','npmInstall', 'npmTest'])

task registroLivreJar(type: Copy, dependsOn: ['clean', 'assemble']) {
    from jar.archivePath
    into project.rootDir
    rename {
        'registrolivre.jar'
    }
}

registroLivreJar.mustRunAfter(clean)

clean << {
    project.file('registrolivre.jar').delete()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

test {

    clean.execute()
    testLogging.showStandardStreams = true

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}
